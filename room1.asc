// room script file

function hGlowingOrb_Look(Hotspot *thisHotspot, CursorMode mode)
{
  player.Say("It is the second best glowing orb that I've seen today.");
}

void _ResetCamera(Camera* cam) {
  cam.Width = Screen.Viewport.Width;
  cam.Height = Screen.Viewport.Height;
  cam.X = 0;
  cam.Y = 0;
}

float _GetDistance(int x0, int y0, int x1, int y1) {
  return Maths.Sqrt(
    Maths.RaiseToPower(IntToFloat(x1 - x0), 2.0) +
    Maths.RaiseToPower(IntToFloat(y1 - y0), 2.0)
  );
}

void _SetCamera(int seconds, int toX, int toY, int toWidth, int toHeight) {
  readonly Camera* cam = Game.Camera;
  readonly int loops = GetGameSpeed() * seconds;
  readonly float floops = IntToFloat(loops);
  float dx, dy, dw, dh;
  
  if (toX != 0) dx = IntToFloat(toX) / floops;
  if (toY != 0) dy = IntToFloat(toY) / floops;
  if (toWidth != 0) dw = IntToFloat(toWidth) / floops;
  if (toHeight != 0) dh = IntToFloat(toHeight) / floops;
  
  System.Log(eLogDebug, "%f %f %f %f", dx, dy, dw, dh);
  
  _ResetCamera(cam);
  
  float fx = IntToFloat(cam.X);
  float fy = IntToFloat(cam.Y);
  float fw = IntToFloat(cam.Width);
  float fh = IntToFloat(cam.Height);
  
  StartCutscene(eSkipESCOnly);
  
  for (int i = 0; i < loops; i++) {
    fw -= dw; cam.Width = FloatToInt(fw, eRoundDown);
    fh -= dh; cam.Height = FloatToInt(fh, eRoundDown);
    fx += dx; cam.X = FloatToInt(fx, eRoundDown);
    fy += dy; cam.Y = FloatToInt(fy, eRoundDown);
    
    if (Game.SkippingCutscene) break;
    Wait(1);
  }
  
  EndCutscene();
}

function room_AfterFadeIn()
{
  _SetCamera(5, 0, 0, 320, 0);
  _SetCamera(5, 0, 0, 0, 160);
  _SetCamera(5, 160, 0, 320, 0);  
  _SetCamera(5, 0, 90, 0, 160); 
  _SetCamera(5, 160, 90, 320, 160);
}
